import java.nio.file.Files

plugins {
    id 'java-library'
    id 'com.gradleup.shadow' version '8.3.0' apply false
}

group = 'arcnode'
version = project.mod_version

subprojects {
    group = parent.group
    version = parent.version

    plugins.apply("java")
    plugins.apply("java-library")

    repositories {
        mavenCentral()
        maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.10.0')
        testImplementation 'org.junit.jupiter:junit-jupiter'

        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'
    }

    test {
        useJUnitPlatform()
    }
}

task dist(group: "build") {
    dependsOn ":mod:fabric:build"
    dependsOn ":mod:neoforge:build"
    dependsOn ":server:dist"

    doLast {
        def output = new File(projectDir, "dist").toPath()
        if (!Files.exists(output))
            Files.createDirectory(output)
        def proj = projectDir.toPath()
        Files.copy(proj.resolve("server/build/libs/server-${project.version}-dist.jar"), output.resolve("NullProtect-Server-${project.version}.jar"))
        Files.copy(proj.resolve("mod/neoforge/build/libs/nullprotect-neoforge-${project.version}.jar"), output.resolve("NullProtect-NeoForge-${project.version}.jar"))
        Files.copy(proj.resolve("mod/fabric/build/libs/nullprotect-fabric-${project.version}.jar"), output.resolve("NullProtect-Fabric-${project.version}.jar"))
    }
}
